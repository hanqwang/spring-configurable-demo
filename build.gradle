// Based on https://dzone.com/articles/working-gradle-spring-aspects

buildscript {
  ext {
    springBootVersion = '1.3.0.RELEASE'
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
  repositories {
    jcenter()
  }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'

group = 'com.williewheeler.demos'
version = '0.1.0.SNAPSHOT'

configurations {
  ajc
  aspects
  compile {
    extendsFrom aspects
  }
}

compileJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  
  doLast {
    ant.taskdef(
      resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties",
      classpath: configurations.ajc.asPath
    )
    ant.iajc(
      source: '1.8',
      target: '1.8',
      destDir: sourceSets.main.output.classesDir.absolutePath,
      maxmem: '512m',
      fork: 'true',
      aspectPath: configurations.aspects.asPath,
      sourceRootCopyFilter: '**/*.java',
      classpath: "${configurations.compile.asPath}"
    ) {
      sourceroots {
        sourceSets.main.java.srcDirs.each {
          pathelement(location: it.absolutePath)
        }
      }
    }
  }
}

ext {
  aspectJVersion = '1.8.7'
  lombokVersion = '1.16.6'
  slf4jVersion = '1.7.12'
  springBootVersion = '1.3.0.RELEASE'
  springVersion = '4.2.3.RELEASE'
}

dependencies {
  ajc(
  	"org.aspectj:aspectjtools:${aspectJVersion}"
  )
  aspects(
  	"org.springframework:spring-aspects:${springVersion}"
  )
  compile(
    "org.aspectj:aspectjrt:${aspectJVersion}",
  	"org.projectlombok:lombok:${lombokVersion}",
    "org.slf4j:slf4j-ext:${slf4jVersion}",
    "org.springframework.boot:spring-boot-starter:${springBootVersion}",
    "org.springframework.boot:spring-boot-starter-web:${springBootVersion}",
    "org.springframework:spring-tx:${springVersion}"
  )
}

repositories {
  jcenter()
}
